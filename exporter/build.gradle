plugins {
    id 'java'
    id 'io.quarkus'
    id 'idea'
    id "org.sonarqube" version "4.4.1.3373"
}

repositories {
    mavenCentral()
    mavenLocal()
}

sonar {
    properties {
        property "sonar.projectKey", "storytime_sync-app-functions"
        property "sonar.organization", "storytime"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-amazon-lambda'
    implementation 'io.quarkus:quarkus-rest-client-jackson'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkiverse.amazonservices:quarkus-amazon-dynamodb-enhanced:2.16.0'
    implementation 'software.amazon.awssdk:url-connection-client:2.25.60'
    implementation 'org.apache.commons:commons-lang3:3.14.0'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    implementation 'one.util:streamex:0.8.1'

    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
}

group 'com.github.storytime.lambda.exporter'
version '0.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

tasks.register('copyConfigs') {
    copy {
        from 'configs/samconfig.toml'
        into "build/"
    }

    copy {
        from 'configs/sam.native.yaml'
        into "build/"
    }
}

tasks.register('depSize') {
    doLast {
        final formatStr = "%,10.2f"
        final conf = configurations.default
        final size = conf.collect { it.length() / (1024 * 1024) }.sum()
        final out = new StringBuffer()
        out << 'Total dependencies size:'.padRight(45)
        out << "${String.format(formatStr, size)} Mb\n\n"
        conf.sort { -it.length() }
                .each {
                    out << "${it.name}".padRight(45)
                    out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
                }
        println(out)
    }
}